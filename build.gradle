plugins {
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'net.kyori.indra' version '1.0.2' apply false
    id 'net.kyori.indra.checkstyle' version '1.0.2' apply false
    id 'net.kyori.indra.publishing' version '1.0.2' apply false
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id 'java'
}

import org.apache.tools.ant.filters.ReplaceTokens
import net.kyori.indra.data.License

allprojects {
    group = 'net.essentialsx'
    version = '2.19.0-SNAPSHOT'
}

def commitsSinceLastTag() {
    def tags = grgit.tag.list().stream().map({it.commit}).toList()
    def commit = grgit.head()
    def depth = 0
    while (true) {
        if (tags.contains(commit))
            return depth
        depth++
        commit = grgit.resolve.toCommit(commit.parentIds.get(0))
    }
}

ext {
    GIT_COMMIT = grgit.head().abbreviatedId
    GIT_DEPTH = commitsSinceLastTag()

    fullVersion = "${version}+${GIT_DEPTH}.${GIT_COMMIT}".replace("-SNAPSHOT", "")

    checkstyleVersion = '8.36.2'
    spigotVersion = '1.16.4-R0.1-SNAPSHOT'
    junit5Version = '5.7.0'
    mockitoVersion = '3.2.0'
}

subprojects {
    apply plugin: 'net.kyori.indra'
    apply plugin: 'net.kyori.indra.checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://ci.ender.zone/plugin/repository/everything/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url = 'https://papermc.io/repo/repository/maven-public/' }
        maven {
            url = 'https://jitpack.io'
            content {
                includeGroup "com.github.milkbowl"
            }
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junit5Version}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"

        if (project.name != "1_8Provider" && project.name != "PaperProvider" && project.name != "NMSReflectionProvider") { // These providers use their own bukkit versions
            api "org.spigotmc:spigot-api:${spigotVersion}"
        }
    }

    // Version Injection
    processResources {
        filter(ReplaceTokens, beginToken: '${',
                endToken: '}', tokens: ["full.version": fullVersion])
    }

    indra {
        checkstyle = "$checkstyleVersion"

        github('EssentialsX', 'Essentials')
        license = new License(
                'GPL-3.0-only',
                'GNU General Public License version 3',
                'https://opensource.org/licenses/GPL-3.0'
        )
    }

    // Deployment only
    if (project.hasProperty('essxUsername') && project.hasProperty('essxPassword')) {
        apply plugin: 'net.kyori.indra.publishing'

        indra {
            publishReleasesTo('essx', 'https://repo.essentialsx.net/releases/')
            publishSnapshotsTo('essx', 'https://repo.essentialsx.net/snapshots/')

            configurePublications {
                pom {
                    description = 'The essential plugin suite for Minecraft servers.'
                    url = 'https://essentialsx.net'
                    developers {
                        developer {
                            id = 'mdcfe'
                            name = 'MD'
                            email = 'md@n3fs.co.uk'
                        }
                        developer {
                            id = 'pop4959'
                        }
                        developer {
                            id = 'JRoy'
                            name = 'Josh Roy'
                        }
                    }
                    ciManagement {
                        system = 'Jenkins'
                        url = 'https://ci.ender.zone/job/EssentialsX'
                    }
                }
            }
        }
    }

    tasks.withType(Sign) {
        onlyIf {
            (project.hasProperty("forceSign") || gradle.taskGraph.hasTask(':publish')) && !project.version.endsWith('-SNAPSHOT')
        }
    }

    javadoc {
        title = "${project.name} API (v${rootProject.ext.fullVersion})"
        options.links(
                'https://hub.spigotmc.org/javadocs/spigot/'
        )
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    // undo https://github.com/KyoriPowered/indra/blob/master/indra-common/src/main/kotlin/net/kyori/indra/IndraPlugin.kt#L57
    archivesBaseName = project.name

    tasks.withType(Jar) {
        archiveVersion.set(fullVersion)
    }
}

def outputTasks() {
    [":EssentialsX:shadowJar", ":EssentialsXAntiBuild:jar", ":EssentialsXChat:jar",
     ":EssentialsXGeoIP:shadowJar", ":EssentialsXProtect:jar", ":EssentialsXSpawn:shadowJar",
     ":EssentialsXXMPP:shadowJar"].stream().map({ tasks.findByPath(it) })
}

task copyToJars(type: Copy) {
    outputTasks().forEach {
        from(it)
    }

    rename '(.*)-all.jar', '$1.jar'

    into file('jars')
}

task cleanJars() {
    delete file('jars')
}

build.dependsOn copyToJars
clean.dependsOn cleanJars
copyToJars.dependsOn tasks.findByPath(":EssentialsX:processResources")
